# Hierarchical MMM for PairsRSS - Version 2: Linear effects on parameters a and c 

model{
   # Likelihood

   for (i in 1:nsubj) {
      for (c in 1:nconds)  {
      	k[c, i, 1:3] ~ dmulti(P[c, i, 1:3], N)
      	loglik[c, i] <- logdensity.multi(k[c, i, 1:3], P[c, i, 1:3], N)
      }
   }

  for (i in 1:nsubj)  {
        # draw individual subject parameters    
      muPi[i] ~ dnorm(meanPi, lambdaPi)
      muPb[i] ~ dnorm(meanPb, lambdaPb)
      slopePi[i] ~ dnorm(dPi, lambdaPi)
      slopePb[i] ~ dnorm(dPb, lambdaPb)
    for (ss in 1:nsetsize) {
      # linear model on Pi and Pb
      logitPi[i,ss] <- muPi[i] + slopePi[i] * Csetsize[ss]
      logitPb[i,ss] <- muPb[i] + slopePb[i] * Csetsize[ss]
	  Pi[i,ss] <- 1/(1+exp(-logitPi[i,ss]))
  	  Pb[i,ss] <- 1/(1+exp(-logitPb[i,ss]))
    }
 
    # compute response probabilities
    
    for (c in 1:nconds) {
    
	  P[c,i,1] <- Pb[i,SSidx[c]] + 
					(1-Pb[i,SSidx[c]]) * Pi[i,SSidx[c]] * GcorrPi[c] + 
					(1-Pb[i,SSidx[c]]) * (1-Pi[i,SSidx[c]]) * GcorrNoPi[c]
	  P[c,i,2] <- (1-Pb[i,SSidx[c]]) * Pi[i,SSidx[c]] * (1-GcorrPi[c]) + 
					(1-Pb[i,SSidx[c]]) * (1-Pi[i,SSidx[c]]) * (1 - GcorrNoPi[c]) * GotherNoPi[c]
	  P[c,i,3] <-   (1-Pb[i,SSidx[c]]) * (1-Pi[i,SSidx[c]]) * (1 - GcorrNoPi[c]) * (1 - GotherNoPi[c])
    
    }

  }
    

# Priors

# Allow separate group level parameters Pi and Pb for each set size
  meanPi ~ dnorm(0, 0.1)
  meanPb ~ dnorm(0, 0.1)
  dPi ~ dnorm(0, 0.1)
  dPb ~ dnorm(0, 0.1)
  sgPi ~ dgamma(1,0.1)
  sgPb ~ dgamma(1,0.1)
  lambdaPi <- 1/pow(sgPi,2)
  lambdaPb <- 1/pow(sgPb,2)


}